include(YACMACompilerLinkerSettings)

# We have some multithreaded tests, so let's activate threading support.
set(THREADS_PREFER_PTHREAD_FLAG YES)
find_package(Threads REQUIRED)
unset(THREADS_PREFER_PTHREAD_FLAG)

function(ADD_MPPP_TESTCASE arg1)
    add_executable(${arg1} ${arg1}.cpp)
    target_link_libraries(${arg1} mp++ Threads::Threads)
    target_compile_options(${arg1} PUBLIC "$<$<CONFIG:DEBUG>:${YACMA_CXX_FLAGS_DEBUG}>" "$<$<CONFIG:RELEASE>:${YACMA_CXX_FLAGS}>")
    if(YACMA_COMPILER_IS_MSVC)
        # Disable the idiotic minmax macros on MSVC, some annoying warnings,
        # and enable the bigobj option.
        target_compile_options(${arg1} PUBLIC "-DNOMINMAX" "/wd4459" "/wd4127" "/bigobj")
        if(MPPP_MSVC_STATIC_RUNTIME)
            target_compile_options(${arg1} PUBLIC "$<$<CONFIG:DEBUG>:-MTd>" "$<$<CONFIG:RELEASE>:-MT>")
        endif()
    endif()
    set_property(TARGET ${arg1} PROPERTY CXX_STANDARD 11)
    set_property(TARGET ${arg1} PROPERTY CXX_STANDARD_REQUIRED YES)
    set_property(TARGET ${arg1} PROPERTY CXX_EXTENSIONS NO)
    add_test(${arg1} ${arg1})
endfunction()

ADD_MPPP_TESTCASE(test_abs)
ADD_MPPP_TESTCASE(test_add_ui)
ADD_MPPP_TESTCASE(test_arith)
ADD_MPPP_TESTCASE(test_arith_ops)
ADD_MPPP_TESTCASE(test_basic)
ADD_MPPP_TESTCASE(test_bin)
ADD_MPPP_TESTCASE(test_divexact)
ADD_MPPP_TESTCASE(test_even_odd)
ADD_MPPP_TESTCASE(test_fac)
ADD_MPPP_TESTCASE(test_gcd)
ADD_MPPP_TESTCASE(test_hash)
ADD_MPPP_TESTCASE(test_nextprime)
ADD_MPPP_TESTCASE(test_pow)
ADD_MPPP_TESTCASE(test_probab_prime_p)
ADD_MPPP_TESTCASE(test_rel)
ADD_MPPP_TESTCASE(test_sqrt)
